grammar uk.ac.kcl.mde.Rinform with org.eclipse.xtext.common.Terminals

generate rinform "http://www.ac.uk/kcl/mde/Rinform"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ReverseInformProgram:
	sentences+=SentencePart*
;

SentencePart:
	RoomDeclaration | 
	RoomAlias |
	RoomDescription |
	ItemDeclaration |
	ItemDescription |
	CharacterDeclaration |
	CharacterDescription |
	Words |
	DirectionStatement
;

RoomDeclaration:
	"r-" name = Text "-r"
;

RoomAlias:
	"-r" room = [RoomDeclaration] "-a" aliases += Text+
;

RoomDescription:
	 "{"roomDescription += (Text)+ "!r""[" room = [RoomDeclaration|Text] "]""}"
;

ContainerDeclaration:
	"c-" name = Text "-c" "!r" "[" room = [RoomDeclaration|Text] "]"
;

ItemInRoomDeclaration:
	"i-" name = Text "-i" "!r" "[" room = [RoomDeclaration|Text] "]"
;

ItemInContainerDeclaration:
	"i-" name = Text "-i" "!c" "[" container = [ContainerDeclaration] "]"
;

ItemDeclaration:
	ItemInRoomDeclaration | ContainerDeclaration | ItemInContainerDeclaration
;

ItemDescription:
	"{"itemDescription += (Text)+ "!i" "[" item = [ItemDeclaration|Text] "]" "}"
;

CharacterDeclaration:
	"p-" name = Text "-p" "!r" "[" room = [RoomDeclaration|Text] "]"
;

CharacterDescription:
	"{"personDescription += (Text)+ "!p" "[" person = [CharacterDeclaration|Text] "]" "}"
;

DirectionStatement:
	"<"room1 = [RoomDeclaration|Text] direction = Direction room2 = [RoomDeclaration|Text]">"
;

enum Direction:
	South | North | East | West | Below | Above
;

Text returns ecore::EString:
	(ID | WS | SYMBOL)+
;

Words: 
	words += Text+
;

SYMBOL returns ecore::EString hidden():
	('.' | ',' | ';' | ':' | '-' | '!' | '?' | '\'' | '(' | ')' | '\"')
;
